;;Copyright 2014 Hari Sundaram , Kaushik Sarkar, Anindita Dey, Arizona State University

;;This file is part of NetDemics.

;;NetDemics is free software: you can redistribute it and/or modify
;;it under the terms of the GNU Lesser General Public License as published by
;;the Free Software Foundation, either version 3 of the License, or
;;(at your option) any later version.

;;Foobar is distributed in the hope that it will be useful,
;;but WITHOUT ANY WARRANTY; without even the implied warranty of
;;MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
;;GNU Lesser General Public License for more details.

;;You should have received a copy of the GNU Lesser General Public License
;;along with Foobar.  If not, see <http://www.gnu.org/licenses/>.


;; Generic setup procedure

globals [
  ;;coloring variables ;;
  neutral
  base
  step
  scaling
   
  update-turtle-attr                            ;; used to update the turtle attributes for different duration models, present in network_dynamics.nls
  new-consider                                  ;; used to update the consider list for different duration models, present in network_dynamics.nls
  after-adoption                                ;; used to update the turtle state after adoption for different duration models, present in network_dynamics.nls
  after-knapsack                                ;; used to update the turtle attributes for different duration models, present in network_dynamics.nls
]

to generic-setup [network-generation-task]
  clear-all
  
  set-coloring-variables
  
  random-seed 12345 + rand-seed-network
  run network-generation-task
  
  set-behaviors
  
  set-diffusion-model
  
  set-edge-weights
  
  random-seed 123456 + rand-seed-resource
  set-resource
  set-remaining-resource
  
  random-seed 1234567 + rand-seed-threshold
  set-thresholds
  
  set-tick-counter                ;; set the tick counter for every behavior
  set-remaining-infected-duration ;; set remaining-duration to all zeros
  set-remaining-recovered-duration ;; set remaining-incubationduration to all zeros
  set-state ;; set S,I,R state
  set-actives
  set-weight-sums
  set-active-neighbors
  set-total-influence
  set-payoffs
  set-consider 
  set-stable-costs               ;; sets all the stable behavior costs for each turtle
  
  set-infected-durations                    ;; sets all the behavior durations for sticky behaviors
  set-recovered-durations         ;; sets all the incubation durations for sticky behaviors
  set-positive-global-effect       ;; set all the positive global effects to true now       
  
  random-seed 12345 + rand-seed-network
  select-seeds
  
  update-seeds
  
  set-turtle-color
  
  setup-indicators
  
  reset-ticks
end

to set-turtle-color
  ask turtles [
    set-color
  ]
end

to update-seeds
  ask turtles with [active?] [
    foreach behav-id-list [
      if array:item actives? ? [
        (run after-adoption ?)
      ]
    ]
  ]
end

to set-diffusion-model
  ifelse duration-model = "SIRS" [
    set-SIRS
  ]
  [
    ifelse duration-model = "SIR" [
      set-SIR
    ]
    [
      if duration-model = "sticky-behavior" [
        set-sticky
      ]
    ]
  ]
  if duration-model = "SIvRS" [
    set-SIvRS
  ]
  
  if duration-model = "none" [
    set-none
  ]
end

to set-SIRS
  set update-turtle-attr task [update-turtle-attr-SIRS]
  set new-consider task [new-consider-SIRS]
  set after-adoption task [after-adoption-SIRS ?]  
  set after-knapsack task [after-knapsack-SIRS]
end

to set-SIvRS
  set update-turtle-attr task [update-turtle-attr-SIvRS]
  set new-consider task [new-consider-SIvRS]
  set after-adoption task [after-adoption-SIvRS ?]  
  set after-knapsack task [after-knapsack-SIvRS]
end

to set-none
  set update-turtle-attr task [update-turtle-attr-none]
  set new-consider task [new-consider-none]
  set after-adoption task [after-adoption-none ?]
  set after-knapsack task [after-knapsack-none]
end

to set-SIR
  set update-turtle-attr task [update-turtle-attr-SIR]
  set new-consider task [new-consider-SIR]
  set after-adoption task [after-adoption-SIR ?]
  set after-knapsack task [after-knapsack-SIR]
end

to set-sticky
  set update-turtle-attr task [update-turtle-attr-sticky]
  set new-consider task [new-consider-sticky]
  set after-adoption task [after-adoption-sticky ?]
  set after-knapsack task [after-knapsack-sticky]
end

to set-coloring-variables
  set neutral white
  set base red
  set step 20
  set scaling (base + num-behaviors * step) - (base + base + step)
end